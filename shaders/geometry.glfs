#version 330 core
layout (location = 0) out vec3 gPosition;
layout (location = 1) out vec3 gNormal;
layout (location = 2) out vec4 gAlbedo;
layout (location = 3) out vec4 gLightmap;
layout (location = 4) out vec3 gViewDir;

in vec3 FragPos;
in vec3 color;
in vec2 TexCoord;
in vec3 surfaceNormal;
in vec3 TangentViewPos;
in vec3 TangentFragPos;
in mat3 TBN;

struct Material {
	sampler2D diffuseTexture; // texture sampler
	sampler2D lightmap;
	bool existsLightmap;
	sampler2D normalmap;
	bool existsNormalmap;
	sampler2D displacementmap;
	bool existsDisplacementmap;
	float shininess;
};
uniform Material material;

const vec3 transparentColor = vec3(1.0, 0.0, 1.0); // pure magenta for transparency
uniform int userMode;

uniform int frameIndex;
uniform int frameRows;
uniform int frameCols;

vec2 frameSize = vec2(1.0f / frameCols, 1.0f / frameRows);
int frameCol = frameIndex % frameCols;
int frameRow = frameIndex / frameRows;
vec2 offset = vec2(frameCol * frameSize.x, frameRow * frameSize.y);
vec2 adjustedTexCoord = TexCoord * frameSize + offset;

uniform vec3 viewPos;

const float heightScale = 0.01;

vec2 DisplacementMapping(vec2 texCoords, vec3 viewDir)
{
	// number of depth layers
	const float minLayers = 8.0;
	const float maxLayers = 32.0;
	float numLayers = mix(maxLayers, minLayers, max(dot(vec3(0.0, 0.0, 1.0), viewDir), 0.0));
	// calculate the size of each layer
	float layerDepth = 1.0 / numLayers;
	// depth of current layer
	float currentLayerDepth = 0.0;
	// the amount to shift the texture coordinates per layer (from vector P)
	vec2 P = viewDir.xy * heightScale;
	vec2 deltaTexCoords = P / numLayers;

	// get initial values
	vec2 currentTexCoords = texCoords;
	float currentDepthMapValue = texture(material.displacementmap, currentTexCoords).r;
	
	while(currentLayerDepth < currentDepthMapValue)
	{
		// shift texture coordinates along direction of P
		currentTexCoords -= deltaTexCoords;
		// get depthmap value at current texture coordinates
		currentDepthMapValue = texture(material.displacementmap, currentTexCoords).r;
		// get depth of next layer
		currentLayerDepth += layerDepth;
	}

	// get texture coordinates before collision (reverse operations)
	vec2 prevTexCoords = currentTexCoords + deltaTexCoords;

	// get depth after and before collision for linear interpolation
	float afterDepth  = currentDepthMapValue - currentLayerDepth;
	float beforeDepth = texture(material.displacementmap, prevTexCoords).r - currentLayerDepth + layerDepth;

	// interpolation of texture coordinates
	float weight = afterDepth / (afterDepth - beforeDepth);
	vec2 finalTexCoords = prevTexCoords * weight + currentTexCoords * (1.0 - weight);

	return finalTexCoords;
}

void main()
{
	vec2 displacedTextCoord = adjustedTexCoord;
	if (material.existsDisplacementmap) {
		vec3 tangentViewDir = normalize(TangentViewPos - TangentFragPos);
		displacedTextCoord = DisplacementMapping(displacedTextCoord, tangentViewDir);
	}

	vec4 diffuse = texture(material.diffuseTexture, displacedTextCoord);

	// discard if diffuse per-fragment color is pure magenta which is considered transparent
	if (userMode == 0 && diffuse.rgb == transparentColor) {
		discard;
	// else store the diffuse per-fragment color
	}else {
		gAlbedo.rgb = diffuse.rgb;
		gAlbedo.a = 1.0;
	}

	gViewDir = normalize(viewPos - FragPos);

	// store the fragment position vector in the first gbuffer texture
	gPosition = FragPos;

	if (!material.existsNormalmap && !material.existsDisplacementmap) {
		// also store the per-fragment normals into the gbuffer
		gNormal = normalize(surfaceNormal);

		// to debug normals
		//vec3 tempNormal = normalize(surfaceNormal);
		//gAlbedo.rgb = tempNormal * 0.5 + 0.5;

	}else {
		// obtain normal from normal map in range [0,1]
		vec3 normal = texture(material.normalmap, displacedTextCoord).rgb;
		// transform normal vector to range [-1,1]
		normal = normalize(normal * 2.0 - 1.0);
		normal = normalize(TBN * normal);
		//normal.y = -normal.y;
		gNormal = normal;

		// to debug normals
		//gAlbedo.rgb = normal * 0.5 + 0.5;
	}

	// and the lightmap per-fragment color
	if (material.existsLightmap) {
		gLightmap.rgb = texture(material.lightmap, displacedTextCoord).rgb;
	}else {
		gLightmap = vec4(1.0, 1.0, 1.0, 1.0);
	}

	// to debug tangents
	//gAlbedo = vec4(normalize(TBN[0]) * 0.5 + 0.5, 1.0); // Visualize tangent as RGB


}