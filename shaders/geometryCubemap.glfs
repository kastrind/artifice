#version 330 core
layout (location = 0) out vec3 gPosition;
layout (location = 1) out vec3 gNormal;
layout (location = 2) out vec4 gAlbedo;
layout (location = 3) out vec4 gLightmap;
layout (location = 4) out vec3 gViewDir;

in vec3 FragPos;
in vec3 color;
in vec3 TexCoord;
in vec3 surfaceNormal;
in vec3 TangentViewPos;
in vec3 TangentFragPos;
in mat3 TBN;

struct Material {
	samplerCube diffuseTexture; // texture sampler
	samplerCube lightmap;
	bool existsLightmap;
	samplerCube normalmap;
	bool existsNormalmap;
	samplerCube displacementmap;
	bool existsDisplacementmap;
	float shininess;
};
uniform Material material;

uniform int userMode;

uniform vec3 viewPos;

const float heightScale = 0.01;

vec2 DisplacementMapping(vec3 texCoords, vec3 viewDir)
{
	// number of depth layers
	const float minLayers = 8.0;
	const float maxLayers = 32.0;
	float numLayers = mix(maxLayers, minLayers, max(dot(vec3(0.0, 0.0, 1.0), viewDir), 0.0));
	// calculate the size of each layer
	float layerDepth = 1.0 / numLayers;
	// depth of current layer
	float currentLayerDepth = 0.0;
	// the amount to shift the texture coordinates per layer (from vector P)
	vec2 P = viewDir.xy * heightScale;
	vec2 deltaTexCoords = P / numLayers;

	// get initial values
	vec2 currentTexCoords = texCoords.xy;
	float currentDepthMapValue = texture(material.displacementmap, vec3(currentTexCoords, texCoords.z)).r;
	
	while(currentLayerDepth < currentDepthMapValue)
	{
		// shift texture coordinates along direction of P
		currentTexCoords -= deltaTexCoords;
		// get depthmap value at current texture coordinates
		currentDepthMapValue = texture(material.displacementmap, vec3(currentTexCoords, texCoords.z)).r;
		// get depth of next layer
		currentLayerDepth += layerDepth;
	}

	// get texture coordinates before collision (reverse operations)
	vec2 prevTexCoords = currentTexCoords + deltaTexCoords;

	// get depth after and before collision for linear interpolation
	float afterDepth  = currentDepthMapValue - currentLayerDepth;
	float beforeDepth = texture(material.displacementmap, vec3(prevTexCoords, texCoords.z)).r - currentLayerDepth + layerDepth;

	// interpolation of texture coordinates
	float weight = afterDepth / (afterDepth - beforeDepth);
	vec2 finalTexCoords = prevTexCoords * weight + currentTexCoords * (1.0 - weight);

	return finalTexCoords;
}


void main()
{
	gViewDir = normalize(viewPos - FragPos);

	vec3 tempTextCoord = normalize(TexCoord);
	vec3 displacedTextCoord = tempTextCoord;
	if (material.existsDisplacementmap) {
		vec3 tangentViewDir = normalize(TangentViewPos - TangentFragPos);
		displacedTextCoord = vec3(DisplacementMapping(displacedTextCoord, tangentViewDir), tempTextCoord.z);
	}

	vec4 diffuse = texture(material.diffuseTexture, displacedTextCoord);

	gAlbedo.rgb = diffuse.rgb;
	gAlbedo.a = 1.0;

	// store the fragment position vector in the first gbuffer texture
	gPosition = FragPos;

	if (!material.existsNormalmap && !material.existsDisplacementmap) {
		// also store the per-fragment normals into the gbuffer
		gNormal = normalize(surfaceNormal);
	}else {
		// obtain normal from normal map in range [0,1]
		vec3 normal = texture(material.normalmap, displacedTextCoord).rgb;
		// transform normal vector to range [-1,1]
		normal = normalize(normal * 2.0 - 1.0);
		normal = TBN * normal;
		normal.y = -normal.y;
		gNormal = normal;
	}

	// and the lightmap per-fragment color
	if (material.existsLightmap) {
		gLightmap.rgb = texture(material.lightmap, displacedTextCoord).rgb;
	}else {
		gLightmap = vec4(1.0, 1.0, 1.0, 1.0);
	}

}